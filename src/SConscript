import SCons
import os

# prepare the Install function with possible permission override
# copied from: https://bitbucket.org/scons/scons/wiki/InstallTargets
from SCons.Script.SConscript import SConsEnvironment
SConsEnvironment.Chmod = SCons.Action.ActionFactory(os.chmod,
        lambda dest, mode: 'Chmod("%s", 0%o)' % (dest, mode))

def InstallPerm(env, dest, files, perm = 0644):
    obj = env.Install(dest, files)
    for i in obj:
        env.AddPostAction(i, env.Chmod(str(i), perm))
    return dest

# put this function "in" scons
SConsEnvironment.InstallPerm = InstallPerm

# libmosaic build script
Import (['env', 'curs_env', 'cat_env'])

# the mosaic libraries
color = curs_env.SharedLibrary ('libmosaic_color', 'color.c')
mosaic = env.SharedLibrary ('libmosaic', 'mosaic.c')
stream_io = env.SharedLibrary ('libmosaic_stream_io', 'stream_io.c')

# the moscat application
cat_src = 'moscat.c'
moscat = cat_env.Program ('moscat', cat_src)

Default ([mosaic, color, stream_io, moscat])

## INSTALL STUFF ##
# includes -> *.h
includes = Glob ('#include/*.h')

# pkg-config files -> *.pc
pkgconfigs = Glob ('#pkgconfigs/*.pc')

env.InstallPerm ('/usr/bin', moscat, 0755)
env.InstallPerm ('/usr/lib', [mosaic, color, stream_io])
env.InstallPerm ('/usr/include/mosaic', includes)
env.InstallPerm ('/usr/share/pkgconfig', pkgconfigs)
# targets: install{,-bin,-lib,-include}
env.Alias ('install-lib', '/usr/lib')
env.Alias ('install-bin', '/usr/bin')
env.Alias ('install-include', '/usr/include')
env.Alias ('install-pkgconfig', '/usr/share/pkgconfig')
env.Alias ('install', ['install-lib', 'install-include', 'install-bin', 'install-pkgconfig'])
